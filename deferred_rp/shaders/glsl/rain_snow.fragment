// __multiversion__
// This signals the loading code to prepend either #version 100 or #version 300 es as apropriate.

#include "fragmentVersionSimple.h"

precision highp float;

#include "uniformMacro.h"
#include "uniformPerFrameConstants.h"
#include "quadSwizzle.glsl"

LAYOUT_BINDING(0) uniform sampler2D TEXTURE_0;
LAYOUT_BINDING(1) uniform sampler2D TEXTURE_1;
LAYOUT_BINDING(2) uniform sampler2D TEXTURE_2;

varying vec2 uv;
varying vec4 color;
varying vec4 worldPosition;
varying vec4 fogColor;

void main()
{
	vec4 albedo = texture2D( TEXTURE_0, uv);

#ifdef ALPHA_TEST
	if (albedo.a < 0.5)
		discard;
#endif

	albedo.a *= color.a;

	vec2 occlusionUV = worldPosition.xz;
	vec4 occlusionTexture = texture2D( TEXTURE_1, occlusionUV);

#ifndef FLIP_OCCLUSION
#define OCCLUSION_OPERATOR <
#else
#define OCCLUSION_OPERATOR >
#endif

#define OCCLUSION_LUMINANCE occlusionTexture.r
#define OCCLUSION_HEIGHT_ONES occlusionTexture.g
#define OCCLUSION_HEIGHT_TENS occlusionTexture.b

	float occlusion_height = OCCLUSION_HEIGHT_ONES + (OCCLUSION_HEIGHT_TENS * 255.0f) - (OCCLUSION_HEIGHT_OFFSET / 255.0f);

#ifndef NO_OCCLUSION
	// clamp the uvs
	if ( occlusionUV.x >= 0.0 && occlusionUV.x <= 1.0 && 
		 occlusionUV.y >= 0.0 && occlusionUV.y <= 1.0 && 
		 worldPosition.y OCCLUSION_OPERATOR occlusion_height) {
		albedo.a = 0.0;
	}
#endif

	float mixAmount = (worldPosition.y - occlusion_height)*25.0;
	vec2 lightingUVs = vec2(OCCLUSION_LUMINANCE, 1.0);
	lightingUVs.x = mix(lightingUVs.x, 0.0, mixAmount);

	vec3 lighting = texture2D( TEXTURE_2, lightingUVs ).rgb;
	
	vec3 swizzlePos = quadSwizzlePos();
	
	int index = quadSwizzleIndex(swizzlePos);
	
	albedo = quadSwizzle0(albedo, swizzlePos.x);
	vec4 finalOutput = albedo;
	
	finalOutput.rgb = 0.5*mix( finalOutput.rgb*lighting.rgb, fogColor.rgb, fogColor.a );

	albedo = 15.0*albedo/31.0;
	
	gl_FragColor = index == 0 ? vec4(0.0, finalOutput.r, albedo.r, finalOutput.a) : index == 1 ? vec4(0.0, finalOutput.g, albedo.g, finalOutput.a) : vec4(0.0, finalOutput.b, albedo.b, finalOutput.a);
}

